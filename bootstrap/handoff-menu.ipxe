#!ipxe
# Smart Boot Script for Remote Servers
# Checks for a boot flag file, defaults to local disk boot
#
# To trigger install remotely:
#   # Install Harvester:
#   echo -e "#!ipxe\ngoto harvester" > /var/www/html/boot-flags/00-21-9b-a1-dc-c0
#
#   # Install Ubuntu:
#   echo -e "#!ipxe\ngoto tinkerbell" > /var/www/html/boot-flags/00-21-9b-a1-dc-c0
#
#   # Then reboot the server
#
# After install completes, delete the flag:
#   rm /var/www/html/boot-flags/00-21-9b-a1-dc-c0

echo
echo ============================================
echo   Colocation PXE Boot
echo   MAC: ${mac}
echo ============================================
echo

set boot_flag_url http://108.181.38.67:8080/boot-flags/${mac:hexhyp}

echo Checking for boot flag at ${boot_flag_url}...
chain ${boot_flag_url} || goto local

:harvester
echo
echo ============================================
echo   Installing Harvester HCI v1.6.1
echo   THIS WILL WIPE THE DISK!
echo ============================================
echo
sleep 3
chain http://108.181.38.67:8081/harvester/harvester.ipxe || goto failed

:tinkerbell
echo
echo ============================================
echo   Installing Ubuntu via Tinkerbell
echo ============================================
echo
set tink_ip tinkerbell.qualityspace.com
set tink_server ${tink_ip}
set grpc_authority ${tink_ip}:42113
set tinkerbell_tls false
chain http://${tink_ip}/ipxe/script/${mac}/auto.ipxe || goto local

:local
echo No boot flag found - booting from local disk...
iseq ${platform} pcbios && sanboot --no-describe --drive 0x80 || exit

:failed
echo Boot failed! Booting from local disk...
sleep 3
goto local
